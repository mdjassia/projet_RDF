BASE <http://example.org/football/>
PREFIX ex:  <http://example.org/football/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?match rdf:type ex:Match ;
         dbo:date ?dateTyped ;
         dbo:homeTeam ?homeTeam ;
         dbo:awayTeam ?awayTeam ;
         dbo:tournament ?tournamentURI ;
         dbo:homeScore ?homeScore ;
         dbo:awayScore ?awayScore ;
         ex:result ?result ;
         dbo:country ?countryURI .
}
WHERE {
  BIND(
    IRI(CONCAT(
      "http://example.org/football/Match_",
      STR(?date), "_",
      REPLACE(?home_team, " ", "_"), "_",
      REPLACE(?away_team, " ", "_")
    )) AS ?match
  )

  BIND(IRI(CONCAT("http://dbpedia.org/resource/", REPLACE(?home_team, " ", "_"))) AS ?homeTeam)
  BIND(IRI(CONCAT("http://dbpedia.org/resource/", REPLACE(?away_team, " ", "_"))) AS ?awayTeam)

  BIND(xsd:date(?date) AS ?dateTyped)
  BIND(xsd:integer(?home_score) AS ?homeScore)
  BIND(xsd:integer(?away_score) AS ?awayScore)

  BIND(dbr:Friendly_match AS ?tournamentURI)
  BIND(IRI(CONCAT("http://dbpedia.org/resource/", REPLACE(?country, " ", "_"))) AS ?countryURI)

  BIND(
    IF(?homeScore > ?awayScore, ?homeTeam,
      IF(?awayScore > ?homeScore, ?awayTeam, ex:Draw)
    ) AS ?result
  )
}
